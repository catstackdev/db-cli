#!/usr/bin/env zsh
# db - universal database cli (v2.0)
# Modular architecture with lazy loading

readonly DB_ROOT="${0:A:h:h}"

# Load core modules
source "$DB_ROOT/lib/core/init.zsh"
source "$DB_ROOT/lib/core/config.zsh"
source "$DB_ROOT/lib/core/helpers.zsh"
source "$DB_ROOT/lib/core/output.zsh"
source "$DB_ROOT/lib/core/cache.zsh"
source "$DB_ROOT/lib/core/transaction.zsh"
source "$DB_ROOT/lib/core/history.zsh"
source "$DB_ROOT/lib/core/repl.zsh"
source "$DB_ROOT/lib/core/migration.zsh"
source "$DB_ROOT/lib/core/schema.zsh"
source "$DB_ROOT/lib/core/backup.zsh"
source "$DB_ROOT/lib/core/seed.zsh"
source "$DB_ROOT/lib/adapters/base.zsh"
source "$DB_ROOT/lib/dispatch.zsh"

# Load user config
db::load_config

# Load plugins
db::load_plugins

# Handle help early (no DB connection needed)
if [[ "${1:-}" == (-h|--help|help|h) ]]; then
  db::load_module "meta"
  cmd::help
  exit 0
fi

# Handle version early
if [[ "${1:-}" == (-V|--version|version) ]]; then
  echo "db $DB_VERSION"
  exit 0
fi

# Parse flags
typeset env_file="${DB_ENV_FILE:-.env}"
typeset direct_url=""
typeset profile=""
typeset -a cmd_args=()

while [[ $# -gt 0 ]]; do
  case "$1" in
    --env=*) env_file="${1#*=}"; shift ;;
    --var=*) DB_URL_VAR="${1#*=}"; shift ;;
    --url=*) direct_url="${1#*=}"; shift ;;
    -p|--profile)
      [[ -z "$2" || "$2" == -* ]] && { db::err "-p requires profile name"; exit 1; }
      profile="$2"; shift 2 ;;
    --profile=*) profile="${1#*=}"; shift ;;
    --format=*) DB_FORMAT="${1#*=}"; shift ;;
    -v|--verbose) DB_VERBOSE=1; shift ;;
    -q|--quiet) DB_QUIET=1; shift ;;
    -w|--wide) DB_COL_WIDTH=0; DB_MAX_COLS=0; shift ;;
    --cols=*) DB_MAX_COLS="${1#*=}"; shift ;;
    --col-width=*) DB_COL_WIDTH="${1#*=}"; shift ;;
    --dry-run) DB_DRY_RUN=1; shift ;;
    *) cmd_args+=("$1"); shift ;;
  esac
done

# Commands that don't need DB connection
case "${cmd_args[1]}" in
  init|config|profiles|bookmarks|bm|save|rm|help|h|-h|--help|version|v|-V|--version|recent|cache-info|cache-clear|cache-stats|migrate|m|schema-version|sv|backup|hist|history|generate)
    db::run "${cmd_args[@]}"
    exit $?
    ;;
esac

# Get URL: profile > direct > env file > auto-detect
if [[ -n "$profile" ]]; then
  DB_URL=$(db::get_profile "$profile") || { db::err "unknown profile: $profile"; exit 1; }
elif [[ -n "$direct_url" ]]; then
  DB_URL="$direct_url"
elif [[ -f "$env_file" ]]; then
  DB_URL=$(db::parse_url "$env_file") || exit 1
elif db::auto_detect; then
  db::dbg "auto-detect: using $DB_ENV_FILE with $DB_URL_VAR"
  DB_URL=$(db::parse_url "$DB_ENV_FILE") || exit 1
else
  db::load_module "meta"
  cmd::help
  echo ""
  echo "${C_DIM}no $env_file in current directory${C_RESET}"
  echo "${C_DIM}use --url=<url> or -p <profile> to specify connection${C_RESET}"
  exit 0
fi

DB_TYPE=$(db::detect "$DB_URL") || exit 1

db::dbg "type=$DB_TYPE url=$(db::mask "$DB_URL")"

# Load adapter
db::load_adapter "$DB_TYPE" || exit 1

# Run command
db::run "${cmd_args[@]}"
